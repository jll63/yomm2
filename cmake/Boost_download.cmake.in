cmake_minimum_required(VERSION 3.5)
include(ExternalProject)
project(Boost)
set(BOOST_VERSION 1.74.0)

list(APPEND BOOST_LIBRARIES
    "assert"
    "config"
    "container_hash"
    "core"
    "detail"
    "dynamic_bitset"
    "functional"
    "integer"
    "move"
    "preprocessor"
    "static_assert"
    "type_traits"
    "throw_exception"
)
if(${CMAKE_VERSION} VERSION_GREATER 3.11)
  set(BOOST_SUBMODULES ${BOOST_LIBRARIES})
    list(TRANSFORM BOOST_SUBMODULES PREPEND libs/)
else()
    foreach(boost_lib ${BOOST_LIBRARIES})
        list(APPEND BOOST_SUBMODULES "libs/${boost_lib}")
    endforeach(boost_lib)
endif()
message(${BOOST_LIBRARIES})
message(${BOOST_SUBMODULES})

ExternalProject_Add(${PROJECT_NAME}
    GIT_REPOSITORY      https://github.com/boostorg/boost.git
    GIT_TAG             boost-${BOOST_VERSION}
    GIT_SUBMODULES      tools/cmake;tools/boost_install;${BOOST_SUBMODULES}
    @ADDITIONAL_GIT_SETTINGS@
    SOURCE_DIR          "@CMAKE_SOURCE_DIR@/dependencies/${PROJECT_NAME}"
    BINARY_DIR          "@CMAKE_CURRENT_BINARY_DIR@/${PROJECT_NAME}_build"
    UPDATE_COMMAND      ""
    DOWNLOAD_COMMAND    ""
    INSTALL_DIR         @DEPENDENCY_INSTALL_PREFIX@
    CMAKE_ARGS          -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
                        -DCMAKE_BUILD_TYPE=Release
                        -DBOOST_ENABLE_CMAKE=ON
                        -DBUILD_TESTING=OFF
)

ExternalProject_Add_Step(${PROJECT_NAME} install_config
    DEPENDEES           install
    COMMAND             ${CMAKE_COMMAND} -E make_directory
                        <INSTALL_DIR>/lib/cmake/Boost-${BOOST_VERSION}
    COMMAND             ${CMAKE_COMMAND} -E copy
                        <SOURCE_DIR>/tools/boost_install/BoostConfig.cmake
                        <INSTALL_DIR>/lib/cmake/Boost-${BOOST_VERSION}
)

# Boost install broken for some CMake versions - do manually!
foreach(boost_lib ${BOOST_LIBRARIES})
  ExternalProject_Add_Step(${PROJECT_NAME} "${boost_lib}"
      DEPENDEES           install
      COMMAND             ${CMAKE_COMMAND} -E copy_directory
                          <SOURCE_DIR>/libs/${boost_lib}/include
                          <INSTALL_DIR>/include
  )
endforeach(boost_lib)

configure_file(
  @CMAKE_SOURCE_DIR@/cmake/BoostConfigVersion.cmake.in
  @DEPENDENCY_INSTALL_PREFIX@/lib/cmake/Boost-${BOOST_VERSION}/BoostConfigVersion.cmake
  @ONLY
)

configure_file(
  @CMAKE_SOURCE_DIR@/cmake/boost_headers-config-version.cmake.in
  @DEPENDENCY_INSTALL_PREFIX@/lib/cmake/boost_headers-${BOOST_VERSION}/boost_headers-config-version.cmake
  @ONLY
)

configure_file(
  @CMAKE_SOURCE_DIR@/cmake/boost_headers-config.cmake.in
  @DEPENDENCY_INSTALL_PREFIX@/lib/cmake/boost_headers-${BOOST_VERSION}/boost_headers-config.cmake
  @ONLY
)
